plugins {
    id 'base'
    id 'org.openapi.generator' version "7.6.0"
}

group = 'de.kristianbergmann'
version = '1.0-SNAPSHOT'

def rootJavaPackage = "de.kristianbergmann"
def todoistOpenApiPath = "$rootDir/../OpenApi/todoist_rest_v2_openapi-3.1.0.yml"

repositories {
    mavenCentral()
}


def kotlinClientDir = "$rootDir/generated/client"
def kotlinClientJavaPackage = "${rootJavaPackage}.todoistclient"
task cleanKotlinClient(type:Delete) {
    delete kotlinClientDir
}
task generateKotlinClient(type:org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    id.set("todoist-rest-v2-client-kotlin")
    version.set("0.2.0")
    generatorName.set("kotlin")
    setInputSpec(file(todoistOpenApiPath).toURI().toString())
    outputDir.set(kotlinClientDir)
    apiPackage.set("${kotlinClientJavaPackage}.api")
    invokerPackage.set("${kotlinClientJavaPackage}.invoker")
    modelPackage.set("${kotlinClientJavaPackage}.model")
    configOptions.put("dateLibrary", "java8")
    configOptions.put("omitGradleWrapper", "true")
}
task buildKotlinClient(type:GradleBuild) {
    dir = file(kotlinClientDir)
    tasks = ['build']
}
tasks.clean.dependsOn cleanKotlinClient
buildKotlinClient.dependsOn tasks.generateKotlinClient
tasks.build.dependsOn buildKotlinClient
